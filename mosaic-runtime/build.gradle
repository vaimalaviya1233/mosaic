import org.jetbrains.kotlin.gradle.plugin.KotlinPlatformType
import org.jetbrains.kotlin.gradle.plugin.mpp.KotlinMetadataTarget

apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'org.jetbrains.kotlin.plugin.compose'
apply from: "$rootDir/addAllTargets.gradle"
apply from: "$rootDir/publish.gradle"
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'dev.drewhamilton.poko'

kotlin {
	applyDefaultHierarchyTemplate()
	explicitApi()

	sourceSets {
		commonMain {
			dependencies {
				api libs.compose.runtime
				api libs.kotlinx.coroutines.core
				implementation libs.mordant
				implementation libs.codepoints
			}
		}
		commonTest {
			dependencies {
				implementation libs.kotlin.test
				implementation libs.kotlinx.coroutines.test
				implementation libs.assertk
			}
		}

		concurrentMain {
			dependsOn(commonMain)
		}
		inputMain {
			dependsOn(commonMain)
		}

		jvmMain {
			dependsOn(concurrentMain)
			dependsOn(inputMain)
			dependencies {
				implementation libs.jansi
			}
		}
		nonJvmMain {
			dependsOn(commonMain)
		}
	}

	targets.each { target ->
		if (target instanceof KotlinMetadataTarget) return
		if (target.platformType != KotlinPlatformType.jvm) {
			target.compilations.main.defaultSourceSet.dependsOn(sourceSets.nonJvmMain)
		}
		if (target.platformType == KotlinPlatformType.native) {
			target.compilations.main.defaultSourceSet.dependsOn(sourceSets.concurrentMain)
			target.compilations.main.defaultSourceSet.dependsOn(sourceSets.inputMain)
		}
	}

	compilerOptions.freeCompilerArgs.add('-Xexpect-actual-classes')
}
